{
  config,
  pkgs,
  ...
}: let
  wallDir = "${config.home.homeDirectory}/Pictures";

  wallpick = pkgs.writeShellScriptBin "wallpick" ''
        set -euo pipefail

        mapfile -t files < <(${pkgs.findutils}/bin/find "${wallDir}" -type f \
          \( -iname "*.jpg" -o -iname "*.png" -o -iname "*.webp" \) | ${pkgs.coreutils}/bin/sort)

        [ "''${#files[@]}" -gt 0 ] || exit 1

        choice="$(printf '%s\n' "''${files[@]}" | ${pkgs.rofi}/bin/rofi -dmenu -i -p "Wallpaper")"
        [ -n "$choice" ] || exit 0

        ${pkgs.swww}/bin/swww img "$choice" --transition-type any --transition-duration 0.6
        ${pkgs.pywal16}/bin/wal -i "$choice" -n
        kitty @ set-colors --all --configured ~/.cache/wal/colors-kitty.conf || true
        for sock in "$XDG_RUNTIME_DIR"/nvim.*; do
          [ -S "$sock" ] || continue
          nvim --server "$sock" --remote-send '<Esc>:silent! colorscheme pywal16<CR>:silent! lua pcall(function() require("lualine").refresh() end)<CR>:silent! Neotree refresh<CR>'
        done

        ${pkgs.pywal16}/bin/wal -i "$choice" -n

        # --- niri: regenerate theme file from pywal ---
        CJSON="$HOME/.cache/wal/colors.json"
        OUT="$HOME/.config/niri/colors.runtime.kdl"
        TMP="$(${pkgs.coreutils}/bin/mktemp)"

        bg="$(${pkgs.jq}/bin/jq -r '.special.background' "$CJSON")"
        c1="$(${pkgs.jq}/bin/jq -r '.colors.color1' "$CJSON")"
        c2="$(${pkgs.jq}/bin/jq -r '.colors.color2' "$CJSON")"
        c4="$(${pkgs.jq}/bin/jq -r '.colors.color4' "$CJSON")"
        c8="$(${pkgs.jq}/bin/jq -r '.colors.color8' "$CJSON")"

        ${pkgs.coreutils}/bin/mkdir -p "$HOME/.config/niri"
        cat >"$TMP" <<EOF
    // generated by wallpick (pywal)
    layout {
      focus-ring {
        width 4
        active-color "$c4"
        inactive-color "$c8"
      }
      border {
        off
        width 4
        active-color "$c1"
        inactive-color "$c8"
        urgent-color "$c2"
      }
    }
    overview {
      backdrop-color "$bg"
    }
    EOF
        ${pkgs.coreutils}/bin/mv "$TMP" "$OUT"
        ${pkgs.niri}/bin/niri msg reload >/dev/null 2>&1 || true
  '';
in {
  home.packages = with pkgs; [
    pywal16
    imagemagick
    swww
    wallpick
    rofi
    nsxiv
  ];

  systemd.user.services.swww = {
    Unit = {
      Description = "swww wallpaper daemon";
      After = ["graphical-session.target"];
      PartOf = ["graphical-session.target"];
    };
    Service = {
      ExecStart = "${pkgs.swww}/bin/swww-daemon";
      Restart = "on-failure";
    };
    Install = {
      WantedBy = ["graphical-session.target"];
    };
  };
}
